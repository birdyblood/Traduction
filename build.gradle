group 'com.michelin.foa'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.google.inject:guice:4.0'
    compile 'org.apache.poi:poi:3.13'
    compile 'com.h2database:h2:1.4.191'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

ext {
    langs = ['fr', 'th', 'pl', 'pt', 'en', 'ru', 'it', 'es', 'de', 'ro', 'hu', 'zh', 'sr', 'es_MX']
    propFiles = ['appCommons', 'appFacesWeb', 'javaxFaces', 'javaxValidation', 'xnetCommons', 'xnetFacesWeb']
}

propFiles.each { propFile ->

    langs.each { lang ->

        task "exportTrad_${propFile}_${lang}"(type: JavaExec) {
            classpath = sourceSets.main.runtimeClasspath

            main = 'com.michelin.foa.traduction.Traduction'

            // arguments to pass to the application
            args propFile, lang
        }
    }
}

propFiles.each { propFile ->
    task "exportTrad_${propFile}" {
        dependsOn langs.collect { lang ->
            tasks["exportTrad_${propFile}_${lang}"]
        }
    }
}

task exportTrad(type: JavaExec) {
    dependsOn propFiles.collect { propFile ->

        langs.collect { lang ->
            tasks["exportTrad_${propFile}_${lang}"]
        }
    }
    enabled = false
}

task createProperties(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.michelin.foa.traduction.CreateProperties'
}

